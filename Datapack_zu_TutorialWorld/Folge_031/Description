##
## Beschreibung zu Folge 031: 
##

- Titel: Minecraft Datapack Tutorial [1.21.10+] #031 - Respawn ohne Bett steuern - Teil 3

- Video: preparing for public

- Unterstützungslink: https://www.amazon.de/hz/wishlist/ls/SG7NGDIC0GEK?ref_=wl_share


## Ziele der Folge:

- Umbauen auf Marcos
- Dorf 2 austatten

## Agenda zum Video:

1. Vorstellung
2. Ziel der Folge
3. Istzustand
4. Forceload
5. Setworldspawn und spawnpoint
6. Verhindern, dass der Spieler seinen Spawnpoint übers Bett steuern kann
7. Steuerung des Teleports von zentralen Respawn
8. Datapack aufräumen
9. Zusammenfassung

##
## ============================================================================================================================================
##

## Links:

Minecraft Wiki:
- https://de.minecraft.wiki/


## Neue Datapack Struktur:

# Datapack Strucktur:
# (+) = Neu
# (>) = Bearbeiten
# (-) = Entfernen
# (*) = Alt/Vorhanden
* Main
    * data
        * respawn
            * function
                * commandblock
                    > check_wirt.mcfunction
                    > interaction.mcfunction
                    > switch.mcfunction
                * dialoge
                    > dialog_wirt.mcfunction
                > teleport.mcfunction

### Bearbeiten:
## check_wirt.mcfunction (Kommenrtare entfernt)
$execute if score _in_work Global_Variables matches 0 unless entity @e[type=minecraft:villager,tag=gasthaus_wirt_$(tagging)] \
    run summon villager $(x) $(y) $(z) \
        {VillagerData:\
            {type:plains,profession:"butcher",level:99},\
            CustomName:[{text:Gastwirt,bold:true}],\
            Invulnerable:1b,\
            NoAI:1b,\
            NoGravity:1b,\
            OnGround:1b,\
            PersistenceRequired:1b,\
            Rotation:[$(rotation),0f],\
            Tags:["gasthaus","gasthaus_wirt","gasthaus_wirt_$(tagging)"]\
        }
$execute if score _in_work Global_Variables matches 0 unless entity @e[tag=gasthaus_interaction_$(tagging)] run \
    summon minecraft:interaction $(x) $(y) $(z) {height:2.0f, width:1.0f, Tags:["gasthaus", "gasthaus_interaction_$(tagging)"]}


## interaction.mcfunction (Kommenrtare entfernt)
$execute as @e[tag=gasthaus_interaction_$(tagging), type=interaction] on target run function respawn:dialoge/dialog_wirt {destination:$(destination)}

$data remove entity @e[tag=gasthaus_interaction_$(tagging), type=interaction, limit=1] attack
$data remove entity @e[tag=gasthaus_interaction_$(tagging), type=interaction, limit=1] interaction


## switch.mcfunction (Kommenrtare entfernt)
$function respawn:commandblock/check_wirt {x:$(x),y:$(y),z:$(z),rotation:$(rotation),tagging:$(tagging)}

$function respawn:commandblock/interaction {tagging:$(tagging),destination:$(destination)}


## dialog_wirt.mcfunction (Kommenrtare entfernt)
scoreboard players enable @s SpawnPoint

$dialog show @s {\
    "type": "minecraft:confirmation",\
    "title": "Gastwirt",\
    "body": {\
        "type": "minecraft:plain_message",\
        "contents": "Möchtest du deinen Respawnpunkt hierher verlegen?"\
    },\
    "inputs": [],\
    "can_close_with_escape": true,\
    "after_action": "close",\
    "yes": {\
        "label": "Ja",\
        "action": {\
            "type": "minecraft:run_command",\
            "command": "trigger SpawnPoint set $(destination)"\
        }\
    },\
    "no": {\
        "label": "Nein",\
        "action": {\
            "type": "minecraft:run_command",\
            "command": "trigger SpawnPoint set 0"\
        }\
    }\
}

## dialog_wirt.mcfunction (Kommenrtare entfernt)
execute if entity @s[tag=spawnpoint_panda_village] run teleport @s 1785 65 268 40 0
execute if entity @s[tag=spawnpoint_panda_village] run return -1

execute if entity @s[tag=spawnpoint_dorf2] run teleport @s 838 65 129 40 0
execute if entity @s[tag=spawnpoint_dorf2] run return -1

teleport @s 1736 65 261 -90 0