##
## Beschreibung zu Folge 038: 
##

- Titel: Minecraft Datapack Tutorial [1.21.11+] #038 - Custom Trading - Teil 4 - Macros + Storage + Scoreboards + Opertation + Give = Kaufen
- Titel: Datapack Tut.[1.21.11+]#038-Custom Trading-T.5 - Tellraw + Storage + Scoreboard = Trading verfeinern

- Video: Preparing

- Unterstützungslink: https://www.amazon.de/hz/wishlist/ls/SG7NGDIC0GEK?ref_=wl_share


## Ziele der Folge:

- Dialog im Verkauf mit Funktion ausrüsten
- Erkennung MAx. Itemanzahl über Kontostand für den Kauf
- Bei Kaufen Items ins Inventar geben und Geld aus dem Geldbeutel entfernen

## Agenda zum Video:

1. Vorstellung
2. Ziel der Folge
3. Istzustand
4. Datapack für e_s__vendors verbessern
5. Formatierte Textausgabe nach Ankauf/Verkauf (tellraw)
6. Begrenzung Ankauf auf ein Stack
7. Begrenzung Verkauf auf ein Stack
8. Testen

##
## ============================================================================================================================================
##

## Links:

Minecraft Wiki:
- https://de.minecraft.wiki/


## Neue Datapack Struktur:

# Datapack Strucktur:
# (+) = Neu
# (>) = Bearbeiten
# (-) = Entfernen
# (*) = Alt/Vorhanden
* Economic_System
    * data
        * e_s__vendors
            * function
				+ misc
					> summon_entities.mcfunction (nur Verschoben)
				+ prices
					> prices.mcfunction (nur Verschoben)
				+ trader1
					> buy.mcfunction
					> buy_apple.mcfunction
                	> dialog.mcfunction (nur Verschoben)
                	> interaction.mcfunction
                	> preparing_dialog.mcfunction
					> reset_trigger.mcfunction (nur Verschoben)
					> sell.mcfuntion
					> sell_rotten_flesh.mcfunction
                	> switch.mcfunction



### Files: (Kommenrtare entfernt)
## buy.mcfunction 

$give @s minecraft:apple $(quantity)

$function e_s__currency:remove_money {loot:$(loot)}

$tellraw @s [\
    {"color":"gold","text":"Farmer: "},\
    {"color":"yellow","text":"Du hast gekauft: $(quantity) Äpfel für $(loot) Groschen"}\
]

function e_s__vendors:trader1/reset_trigger


## buy_apple.mcfunction 
scoreboard objectives add E_S.Temp1 dummy

execute store result score @s E_S.Temp1 run data get storage e_s__vendor:data prices."minecraft:apple".buy
scoreboard players operation @s E_S.Temp1 *= @s E_S.Vendor_Panda_Village_buy_apple

execute store result storage e_s__vendor:data buy.loot int 1 run scoreboard players get @s E_S.Temp1
execute store result storage e_s__vendor:data buy.quantity int 1 run scoreboard players get @s E_S.Vendor_Panda_Village_buy_apple

function e_s__vendors:trader1/buy with storage e_s__vendor:data buy
scoreboard objectives remove E_S.Temp1


## interaction.mcfunction 
execute as @e[tag=E_S.Vendors_Panda_Village_interaction] on target run function e_s__vendors:prices/prices

execute as @e[tag=E_S.Vendors_Panda_Village_interaction] on target run function e_s__vendors:trader1/preparing_dialog
execute as @e[tag=E_S.Vendors_Panda_Village_interaction] on target run function e_s__vendors:trader1/dialog with storage e_s__vendor:data dialog

execute as @e[tag=E_S.Vendors_Panda_Village_interaction] run data remove entity @s interaction
execute as @e[tag=E_S.Vendors_Panda_Village_interaction] run data remove entity @s attack



## preparing_dialog.mcfunction 
scoreboard objectives add E_S.Temp1 dummy
scoreboard objectives add E_S.Temp2 dummy

data modify storage e_s__vendor:data dialog.price_sell set from storage e_s__vendor:data prices."minecraft:rotten_flesh".sell
data modify storage e_s__vendor:data dialog.price_buy set from storage e_s__vendor:data prices."minecraft:apple".buy

execute store result score @s E_S.Temp1 run clear @s rotten_flesh 0
execute if score @s E_S.Temp1 matches 65.. run scoreboard players set @s E_S.Temp1 64
execute store result storage e_s__vendor:data dialog.max_sell int 1 run scoreboard players get @s E_S.Temp1

execute store result score @s E_S.Temp1 run data get storage e_s__vendor:data prices."minecraft:apple".buy
execute store result score @s E_S.Temp2 run scoreboard players get @s Currency_Total

scoreboard players operation @s E_S.Temp2 /= @s E_S.Temp1

execute if score @s E_S.Temp2 matches 65.. run scoreboard players set @s E_S.Temp2 64

execute store result storage e_s__vendor:data dialog.max_buy int 1 run scoreboard players get @s E_S.Temp2 

scoreboard objectives remove E_S.Temp1
scoreboard objectives remove E_S.Temp2

## sell.mcfunction 
$clear @s rotten_flesh $(quantity)
$function e_s__currency:add_money {loot:$(loot)}

$tellraw @s [\
    {"color":"gold","text":"Farmer: "},\
    {"color":"yellow","text":"Du hast verkauft: $(quantity) Verrottetes Fleisch für $(loot) Groschen"}\
]


function e_s__vendors:trader1/reset_trigger


## sell_rotten_flesh.mcfunction 
scoreboard objectives add E_S.Temp1 dummy


#scoreboard players get @s E_S.Vendor_Panda_Village_sell_rotten_flesh
execute store result score @s E_S.Temp1 run data get storage e_s__vendor:data dialog.price_sell
scoreboard players operation @s E_S.Temp1 *= @s E_S.Vendor_Panda_Village_sell_rotten_flesh

execute store result storage e_s__vendor:data sell.loot int 1 run scoreboard players get @s E_S.Temp1
execute store result storage e_s__vendor:data sell.quantity int 1 run scoreboard players get @s E_S.Vendor_Panda_Village_sell_rotten_flesh

function e_s__vendors:trader1/sell with storage e_s__vendor:data sell

data remove storage e_s__vendor:data sell
scoreboard objectives remove E_S.



## switch.mcfunction 
scoreboard objectives add E_S.Vendor_Panda_Village_reset trigger


scoreboard objectives add E_S.Vendor_Panda_Village_sell_rotten_flesh trigger
scoreboard objectives add E_S.Vendor_Panda_Village_buy_apple trigger

data modify storage e_s__vendor:data temp set value ""

execute positioned 1742 65 204 unless entity @e[tag=E_S.Vendors_Panda_Village_villager, distance=..3] run function e_s__vendors:misc/summon_entities
execute positioned 1742 65 204 unless entity @e[tag=E_S.Vendors_Panda_Village_interaction, distance=..3] run function e_s__vendors:misc/summon_entities


function e_s__vendors:trader1/interaction

execute as @a[scores={E_S.Vendor_Panda_Village_reset=1..}] run function e_s__vendors:trader1/reset_trigger

execute as @a[scores={E_S.Vendor_Panda_Village_sell_rotten_flesh=1..}] run function e_s__vendors:trader1/sell_rotten_flesh

execute as @a[scores={E_S.Vendor_Panda_Village_buy_apple=1..}] run function e_s__vendors:trader1/buy_apple

